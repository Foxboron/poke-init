var WIN_CERT_TYPE_PKCS_SIGNED_DATA = 0x0002,
    WIN_CERT_TYPE_EFI_PKCS115      = 0x0EF0,
    WIN_CERT_TYPE_EFI_GUID         = 0x0EF1;

type EFI_TIME =
  struct
  {
    /* // 1900 – 9999 */
    uint16 Year;

    /* // 1 – 12 */
    uint8 Month;

    /* // 1 – 31 */
    uint8 Day;

    /* // 0 – 23 */
    uint8 Hour;

    /* // 0 – 59 */
    uint8 Minute;

    /* // 0 – 59 */
    uint8 Second;

    uint8 Pad1;

    /* // 0 – 999,999,999 */
    uint32 Nanosecond;

    /* // -1440 to 1440 or 2047 */
    uint16 TimeZone;

    uint8 Daylight;

    uint8 Pad2;

     method _print = void:
        {
            printf "%u16d-%u8d-%u8dT%u8d:%u8d:%u8d", Year, Month, Day, Hour, Minute, Second;
        }
};


type EFI_GUID =
  struct {
    uint32 Data1;
    uint16 Data2;
    uint16 Data3;
    uint8[8] Data4;

     method _print = void:
        {
            printf "#<EFI_GUID: %u32x-%u16x-%u16x-%u8x%u8x-%u8x%u8x>", Data1, Data2, Data3, Data4[2], Data4[3], Data4[0], Data4[1];
        }
};

fun fmt_efiguid = (EFI_GUID guid) void {
    printf "EFI_GUID: %u32x-%u16x-%u16x-%u8x%u8x-%u8x%u8x", guid.Data1, guid.Data2, guid.Data3,
                                                            guid.Data4[2], guid.Data4[3],
                                                            guid.Data4[0], guid.Data4[1];
}


type WIN_CERTIFICATE =
  struct {
    offset<uint32,B> dwLength;
    uint16 wRevision;
    uint16 wCertificateType
      : wCertificateType in [WIN_CERT_TYPE_EFI_GUID, WIN_CERT_TYPE_EFI_PKCS115, WIN_CERT_TYPE_PKCS_SIGNED_DATA];

     method _print = void:
        {
            print "#<";
            print "wCertificateType: ";
            if (wCertificateType == WIN_CERT_TYPE_EFI_GUID)
              print "WIN_CERT_TYPE_EFI_GUID ";
            if (wCertificateType == WIN_CERT_TYPE_EFI_PKCS115)
              print "WIN_CERT_TYPE_EFI_PKCS115 ";
            if (wCertificateType == WIN_CERT_TYPE_PKCS_SIGNED_DATA)
              print "WIN_CERT_TYPE_PKCS_SIGNED_DATA ";
            printf "wRevision: %u16d ", wRevision;
            printf "dwLength: %u32d", dwLength/#B;
            print ">";
        }
};

type WIN_CERTIFICATE_UEFI_GUID =
   struct {
     WIN_CERTIFICATE Hdr;
     EFI_GUID CertType;
     uint8[Hdr.dwLength - Hdr'size - CertType'size] CertData;
};

type EFI_VARIABLE_AUTHENTICATION_2 =
  struct {
    EFI_TIME TimeStamp;
    WIN_CERTIFICATE_UEFI_GUID AuthInfo;
};

type EFI_SIGNATURE_LIST =
  struct {
    EFI_GUID SignatureType;
    offset<uint32,B> SignatureListSize;
    uint32 SignatureHeaderSize;
    offset<uint32,B> SignatureSize;
    uint8[SignatureHeaderSize] SignatureHeader;

    type Signature = 
    struct {
        EFI_GUID SignatureOwner;
        uint8[SignatureSize - SignatureOwner'size] SignatureData;
    };

    Signature[SignatureListSize/SignatureSize] Signatures;
  };


type DBX = 
  struct {
    EFI_VARIABLE_AUTHENTICATION_2 Auth;
    EFI_SIGNATURE_LIST[] lists;
  };
